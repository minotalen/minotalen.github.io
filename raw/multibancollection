title Multiban Collection
author Pfeiffer, Egevad, Sever
homepage minotalen.itch.io
sprite_size 10
realtime_interval 0.25
norepeat_action

========
OBJECTS
========

Background
black

TargA
Black White
..........
.1.1.1.1..
........1.
.1........
........1.
.1..1.....
.....1..1.
.1........
........1.
.1.1.1.1..

TargB
Black White
..........
..1.1.1.1.
.1........
........1.
.1..1....
.....1..1.
.1........
........1.
.1........
..1.1.1.1.

allA
transparent

allB
transparent

Wall
Black White
..........
111111111.
111111111.
111111111.
111111111.
..........
111111.111
111111.111
111111.111
111111.111

PlayerDown
Black LightBlue White #ff80d5
..........
..111111..
.33311333.
..101101..
..111111..
.11133111.
1.130031.1
1.131131.1
..111111..
.11....11.

(
PlayerDown2
Black LightBlue White
..........
..........
..111111..
.22111122.
..121121..
.11111111.
..112211..
..120021..
..111111..
..11..11..

PlayerDown3
Black White LightBlue
..........
..........
..........
..111111..
..111111..
.22211222
..101101..
...2222...
..220022..
..222222..
)

PlayerRight
Black LightBlue White  #ff80d5
..........
..111111..
.33331133.
..11.11...
..111111..
111113311.
..113003.1
..113113.1
..111111..
..11...11.

PlayerLeft
Black LightBlue White  #ff80d5
..........
..111111..
.33113333.
...11.11..
..111111..
.113311111
1.3..311..
1.311311..
..111111..
.11...11..

PlayerUp
Black LightBlue White  #ff80d5
..........
..111111..
.31111113.
..111111..
..111111..
.11111111.
1.111111.1
1.111111.1
..111111..
.11....11.


StalePlayer
Black White lightblue  #ff80d5
..........
..........
..........
...1111...
...1111...
..111111..
.33311333.
..101101..
.11300311.
.11111111.

Crate
Black White
..........
..111111..
.1..1...1.
.1.1....1.
.11.....1.
.1......1.
.1.....11.
.1....1.1.
.1...1..1.
..111111..

CrateDun
Black lightblue
..........
..111111..
.11.1...1.
.1.1....1.
.11.....1.
.1......1.
.1.....11.
.1....1.1.
.1...1.11.
..111111..


0
transparent

1
transparent

2
transparent

3
transparent

4
transparent

5
transparent

6
transparent

7
transparent

8
transparent

9
transparent

10
transparent

11
transparent

12
transparent

13
transparent

14
transparent

15
transparent

16
transparent

(anim counter)
A
transparent

B
transparent

C
transparent

D
transparent

E
transparent

F
transparent

G
transparent

H
transparent

voidVoid
black

voidA
black white
..........
..........
..1.......
..........
..........
..........
........1.
..........
..........
..........

voidB
black white
..........
......1...
..........
..........
..........
..........
...1......
..........
..........
..........

voidC
black white
..........
..........
..........
..........
..........
..........
....1.....
..........
..........
..........

voidD
black white
..........
.....1....
..........
..........
..........
..........
........1.
..........
..........
..........

voidE
black white
..........
..........
..........
......1...
..........
..........
..........
..........
.1........
..........

voidF
black white
..........
..........
..........
..........
..........
......1...
..........
..........
..........
..........

=======
LEGEND
=======

Player = PlayerRight or PlayerLeft or PlayerUp or PlayerDown
U = PlayerDown
. = Background
- = voidVoid
voidFill = voidA or voidB or voidC or voidD or voidE or voidF
# = Wall
Target = TargA or TargB
@ = StalePlayer
$ = Crate
* = Crate and TargA
+ = StalePlayer and TargA
T = TargA
Num = 0 or 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or 10 or 11 or 12 or 13 or 14 or 15 or 16
animCount = A or B or C or D or E or F or G or H
targState = allA or allB
=======
SOUNDS
=======

Crate MOVE 36772507
StalePlayer create 19377506
endlevel 33054909
restart 23251705

================
COLLISIONLAYERS
================

Background
Num
animCount, voidVoid, voidFill
Target, targState
StalePlayer, Player, Wall, Crate
CrateDun

======
RULES
======

startLoop
random [ voidVoid ] -> [ voidA ]
random [ voidVoid ] -> [ voidB ]
random [ voidVoid ] -> [ voidC ]
random [ voidVoid ] -> [ voidD ]
random [ voidVoid ] -> [ voidE ]
random [ voidVoid ] -> [ voidF ]
endLoop

(direction feedback)
late [ PlayerLeft no animCount ] -> [ PlayerLeft A ]
late [ PlayerRight no animCount ] -> [ PlayerRight A ]
late [ PlayerUp no animCount ] -> [ PlayerUp A ]
[ PlayerLeft F ] -> [ PlayerDown ]
[ PlayerRight F ] -> [ PlayerDown ]
[ PlayerUp F ] -> [ PlayerDown ]


[ up Player ] -> [ up PlayerUp ]
[ down Player ] -> [ down PlayerDown ]
[ left Player ] -> [ left PlayerLeft ]
[ right Player ] -> [ right PlayerRight ]

(place animation counter)
[ 0 ] [ voidA ]  -> [ 0 ] [ allA A ]

(count animation)
[ H allA ]  -> [ A allB ]
[ H allB ]  -> [ A allA ]
[ G ] -> [ H ]
[ F ] -> [ G ]
[ E ] -> [ F ]
[ D ] -> [ E ]
[ C ] -> [ D ]
[ B ] -> [ C ]
[ A ] -> [ B ]
[ allA ] [ TargB ] -> [ allA ] [ TargA ]
[ allB ] [ TargA ] -> [ allB ] [ TargB ]


[ > Player | StalePlayer ] -> [ StalePlayer | PlayerDown ]
[ > Player | Wall | ... | StalePlayer ] -> [ StalePlayer | Wall | ... | PlayerDown ]
[ > Player | Wall | Crate | StalePlayer ] -> [ StalePlayer | Wall | Crate | PlayerDown ]
[ > Player | Crate | Crate | ... | StalePlayer ] -> [ StalePlayer | Crate | Crate | ...| PlayerDown ]
[ > Player | Crate | Wall | ... | StalePlayer ] -> [ StalePlayer | Crate | Wall | ... | PlayerDown ]
[ > Player | Crate | StalePlayer ] -> [ StalePlayer | Crate | PlayerDown ]

(random [ Target ] -> [ Target 0 ]
)

[ CrateDun ] -> [ ]

(cancel to prevent num and animCount from slipping)
[ > Player | Wall ] -> cancel
[ > Player | StalePlayer ] -> cancel
[ > Player | Crate | Wall ] -> cancel
[ > Player | Crate | StalePlayer ] -> cancel
[ > Player | Crate | Crate ] -> cancel


[ > Player Num no Target animCount ] -> [ > Player > Num ]
late [ Player Num no animCount ] -> [ Player Num A ]
[ > Player Num animCount | Target ] -> [ > Player > Num | Target ]

[ > Player Num ] -> [ > Player > Num ]


[ PlayerDown ] [ 0 ] -> [ PlayerDown 1 ] [  ]
[ 0 ] [ StalePlayer ]                -> [   ] [ PlayerDown 1 ]
random [ 1 ] [ StalePlayer no Num ]  -> [ 1 ] [ StalePlayer 2 ]
random [ 2 ] [ StalePlayer no Num ]  -> [ 2 ] [ StalePlayer 3 ]
random [ 3 ] [ StalePlayer no Num ]  -> [ 3 ] [ StalePlayer 4 ]
random [ 4 ] [ StalePlayer no Num ]  -> [ 4 ] [ StalePlayer 5 ]
random [ 5 ] [ StalePlayer no Num ]  -> [ 5 ] [ StalePlayer 6 ]
random [ 6 ] [ StalePlayer no Num ]  -> [ 6 ] [ StalePlayer 7 ]
random [ 7 ] [ StalePlayer no Num ]  -> [ 7 ] [ StalePlayer 8 ]
random [ 8 ] [ StalePlayer no Num ]  -> [ 8 ] [ StalePlayer 9 ]
random [ 9 ] [ StalePlayer no Num ]  -> [ 9 ] [ StalePlayer 10 ]
random [ 10 ] [ StalePlayer no Num ] ->[ 10 ] [ StalePlayer 11 ]
random [ 11 ] [ StalePlayer no Num ] ->[ 11 ] [ StalePlayer 12 ]
random [ 12 ] [ StalePlayer no Num ] ->[ 12 ] [ StalePlayer 13 ]
random [ 13 ] [ StalePlayer no Num ] ->[ 13 ] [ StalePlayer 14 ]
random [ 14 ] [ StalePlayer no Num ] ->[ 14 ] [ StalePlayer 15 ]
random [ 15 ] [ StalePlayer no Num ] ->[ 15 ] [ StalePlayer 16 ]

[ >  Player | Crate ] -> [  >  Player | > Crate ]

(player switching)
[ action Player 1 ] [ StalePlayer 2 ] -> [ StalePlayer 1 ] [ PlayerDown 2 ]

[ action Player 2 ] [ StalePlayer 3 ] -> [ StalePlayer 2 ] [ PlayerDown 3 ]
[ action Player 2 ] [ StalePlayer 1 ] -> [ StalePlayer 2 ] [ PlayerDown 1 ]

[ action Player 3 ] [ StalePlayer 4 ] -> [ StalePlayer 3 ] [ PlayerDown 4 ]
[ action Player 3 ] [ StalePlayer 1 ] -> [ StalePlayer 3 ] [ PlayerDown 1 ]

[ action Player 4 ] [ StalePlayer 5 ] -> [ StalePlayer 4 ] [ PlayerDown 5 ]
[ action Player 4 ] [ StalePlayer 1 ] -> [ StalePlayer 4 ] [ PlayerDown 1 ]

[ action Player 5 ] [ StalePlayer 6 ] -> [ StalePlayer 5 ] [ PlayerDown 6 ]
[ action Player 5 ] [ StalePlayer 1 ] -> [ StalePlayer 5 ] [ PlayerDown 1 ]

[ action Player 6 ] [ StalePlayer 7 ] -> [ StalePlayer 6 ] [ PlayerDown 7 ]
[ action Player 6 ] [ StalePlayer 1 ] -> [ StalePlayer 6 ] [ PlayerDown 1 ]

[ action Player 7 ] [ StalePlayer 8 ] -> [ StalePlayer 7 ] [ PlayerDown 8 ]
[ action Player 7 ] [ StalePlayer 1 ] -> [ StalePlayer 7 ] [ PlayerDown 1 ]

[ action Player 8 ] [ StalePlayer 9 ] -> [ StalePlayer 8 ] [ PlayerDown 9 ]
[ action Player 8 ] [ StalePlayer 1 ] -> [ StalePlayer 8 ] [ PlayerDown 1 ]

[ action Player 9 ] [ StalePlayer 10 ] -> [ StalePlayer 9 ] [ PlayerDown 10 ]
[ action Player 9 ] [ StalePlayer 1 ] -> [ StalePlayer 9 ] [ PlayerDown 1 ]

[ action Player 10 ] [ StalePlayer 11 ] -> [ StalePlayer 10 ] [ PlayerDown 11 ]
[ action Player 10 ] [ StalePlayer 1 ] -> [ StalePlayer 10 ] [ PlayerDown 1 ]

[ action Player 11 ] [ StalePlayer 12 ] -> [ StalePlayer 11 ] [ PlayerDown 12 ]
[ action Player 11 ] [ StalePlayer 1 ] -> [ StalePlayer 11 ] [ PlayerDown 1 ]

[ action Player 12 ] [ StalePlayer 13 ] -> [ StalePlayer 12 ] [ PlayerDown 13 ]
[ action Player 12 ] [ StalePlayer 1 ] -> [ StalePlayer 12 ] [ PlayerDown 1 ]

[ action Player 13 ] [ StalePlayer 14 ] -> [ StalePlayer 13 ] [ PlayerDown 14 ]
[ action Player 13 ] [ StalePlayer 1 ] -> [ StalePlayer 13 ] [ PlayerDown 1 ]

[ action Player 14 ] [ StalePlayer 15 ] -> [ StalePlayer 14 ] [ PlayerDown 15 ]
[ action Player 14 ] [ StalePlayer 1 ] -> [ StalePlayer 14 ] [ PlayerDown 1 ]

[ action Player 15 ] [ StalePlayer 16 ] -> [ StalePlayer 15 ] [ PlayerDown 16 ]
[ action Player 15 ] [ StalePlayer 1 ] -> [ StalePlayer 15 ] [ PlayerDown 1 ]

[ action Player 16 ] [ StalePlayer 1 ] -> [ StalePlayer 16 ] [ PlayerDown 1 ]

late [ Crate Target ] -> [ Crate Target CrateDun]
==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======
message Alfred Pfeiffer, 2002

..--------------..
.-------.-###----.
-.--#######0##----
----#TTT@@u..#--.-
----#######.##----
----#.....#..#----
----#.$.$..$.#----
-.--#.....#..#----
.---##########-.-.
...------------...

..--------------..
.-----.---###----.
-.--#######.##----
----#T+*@$u..#--.-
----#######.##----
-.--#...###..#----
----#......$.#----
--.-#.#...#..#---.
.---##########---.
...---.--------...

...-------------..
.---##########--..
.---#@u0T.TTT#---.
----###.#.####---.
-.--#........#----
--.-#.#.###$##----
----#.###.#..#----
----#.$.$.$..#----
..--#.....#..#---.
.---##########--..
...------------...

...-----------...
..--#########--..
.---#u0#.#.@#---.
----#.$...$.#----
----##.#T#.##----
----#..T#T..#----
----##.#T#.##----
----#.$...$.#----
----#@.#.#.@#----
----#########---.
...-----------...

..-----------..
.--#########--.
.--#u.#.#..#--.
---#..$....#---
---##$#T#.##---
---#..T.T..#---
---##.#T#$##---
---#....$..#---
---#..#.#.@#---
.--#########--.
..-----------..

..-----------..
..-#########-.-
.--#u..T#.@#--.
---#..#.$..#---
---##$.#.#.#--.
---#T.#.#.T#---
-.-#.#.#.$##---
---#..$.#..#---
---#@.#T..@#-.-
.--#########--.
..-----------..

..---.--------------..
..---####-------####-.
.-####..#########..#-.
--#@.#.............#--
--#@.##.#####.#.#.##--
--#@$.#.#...#####.#---
--#@$.#.##..TTTT#.#---
--#@$.#.###.#####.##--
-.#@$....#.........#--
--#u..#..#..#####..#--
.-###########---####-.
..------------------..

.-------------..
.###########---.
-#....U....#----
-#.#####...###--
-#...#.###.#.#--
-#.$..$@.$.$.#--
-#####@#####.##-
-----#.#TTTT.@#-
.----##########-
...------------.

message oof alarm! - the next 2 variants feel a bit like busywork
..-------###----..
.---#######@##---.
----#TTT.0@$@#----
----#######.##----
----#@@@@@#$@#----
----#@$@@@@@@#----
----#.@@@@#@@#----
.---##########---.
...------------...

..---.--------------..
.----####-------####-.
--####0.#########..#--
--#..#......$.@....#--
--#@.##.#####.#.#.##--
--#...#.#@..#####.#---
--#.$.#.##..*.+.#.#---
.-#@..#.###.#####.##--
--#@.....#..$......#--
--#@..#..#..#####..#--
.-###########---####-.
..-----------.------..

message Sven Egevad 2005

..---------..
.--#######--.
---#T0@.T#---
---#.$.$.#---
---#T#.#T#---
---#.$.$.#---
---###@###---
.----###----.
...-------...

...--------..
.---#####---.
----#+#+#----
---##$$$##---
---#+0$.+#---
---##@#@##---
.---#####---.
...-------...

..---------...
.-#######----.
--#+0...##----
--##$##..###--
--#....#.*@#--
--#...#@*.##--
--###.###.#---
.-#####-###--.
..---------...

..---------..
.----###----.
--####+#-----
--#0.**####--
--#.$.*...#--
--#@..#...#--
.-#########-.
..---------..

.-------------.
-#############-
-#T0..T#T...T#-
-#.$.$.#.$.$.#-
-#T#.#T.T#.#T#-
-#.$.$.#.$.$.#-
-###@#####@###-
.--###---###--.
..-----------..

...--------.
.-----####-.
.---###+@#--
----#@#*@#--
--###0@*@#--
--#@$***@#--
--##@@@@##--
.--######--.
..--------..

..----###----..
.-#####0#####-.
--#@...*...@#--
--#.$.**##$.#--
--#..*#+##..#--
--##**+#+**##--
--#..##+#*..#--
--#.$##*#.$.#--
--#@...*...@#--
.-#####.#####-.
..----###----..

.---------..
.-#######--.
--#+#@#@#---
--#T#$#$##--
--#T#@@$@#--
--#T#$####--
--#T$@@#----
--#0$@+#----
.-######---.
..-------...

..-----------..
.-###########-.
--#T.T.#.T.T#--
--#.$#$#$#$.#--
--#..@@*@...#--
--#.$#$#$#$.#--
--#T.T.#.T.T#--
.-###########-.
..-----------..

message oof
..----------------.---.
.-###################-.
--#T...T#T...T#T...T#--
--#.$.$.#.$.$.#.$.$.#--
--#T#.#T#T#.#T#T#.#T#--
.-#.$.$.#.$.$.#.$.$.#--
--###.#####@#####.###--
----#...@*@*@*@...#----
..--###############---.
...-----.-----------...

..-----------..
.-----#####---.
.-----#0@.#----
----###$.$###--
----#.......#--
--##*******##--
--#.........#--
--#T**###**T#--
--#....@....#--
.-###########-.
..-----------..

...-------...
.---#####---.
----#0@.#----
---##$+$##---
--##..$..##--
--#T##@##T#--
--#.......#--
--#.#####.#--
.-###--####-.
.----------..

..-----------..
.----#####----.
.---##@#@##---.
---##0$#$.##---
---#@..#..@#---
--##*******##--
--#.........#--
--#T**###**T#--
--#....@....#--
.-###########-.
..-----------..

..-.-------.-..
...--#####--...
.----#0@.#----.
----##$+$##----
-.-##..$..##---
--##T##@##+#---
--#...@@@#*#---
--##.##@##.#---
--##.#####.##--
--#.........#--
.-##.#####.##-.
.--###---###--.
..------.---...

..-------------..
.---#########---.
---##+T.@.T+##---
-###.###.###.###-
-#.$.#.#.#.#.$.#-
-#@*...#.#...*@#-
-#.$.#.#.#.#.$.#-
-###.#.#.#.#.###-
---##.......##---
.---####.####---.
..-----###----...
.....-.----......

...--------....
..---#####---..
.----#+#+#----.
---###$$$###---
---#+$+@0$+#---
---##$+*+$##---
---#+$.@+$+#---
---###$$$###---
-----#+#+#-----
.----#####----.
...---------...

....--------....
..---######--...
.--###0...#---..
.--#...##.#----.
---#.#..*.#-----
---#.###*#####--
.--#.##@*@$@.#--
.--#.###T#####--
.--#....*@#----.
.--#####@##---..
..-----###---...
....-------.-...

..------------..
.--##########--.
.--#@0@#..*@#--.
---#*$*.T#$.#---
---#.#.#..*@#---
---#.T.###.##---
---##.###.T.#---
---#@*..#.#.#---
---#.$#T.*$*#---
---#@*..#.@.#---
.--##########--.
..------------..

message Erim Sever 2005

..----------..
.--########--.
---#TT@..T#---
---#$$$$$$#---
---#T0.@TT#---
.--########--.
...--------...

..-----------..
.----####-----.
----##TT##-----
----#0@$@##----
----#T$$$T#----
----##@$@.#----
-----##T.##----
.-----####----.
...---------...

..--######--..
.---#0...#---.
--###..$.###--
--#..$***.@#--
--#.**TT*..#--
--#..*TT**.#--
--#@.***$..#--
--###.$..###--
.---#....#---.
...-######-...

..#####-----------...
.-#0..#####---------.
--#.$.....#--#####---
--#.$#.@#.####T.T###-
--#.$.$##.#.....TTT#-
--###$....##.##T.TT#-
.---#....$...###.###-
----####..##$$...#---
.------#####@#####---
..---------###------.
.....------------....

..--------------------..
..-##################--.
.--#TT0.#..*..#..*..#--.
---#T*T.#..$..#..$..#---
---#T#..#$.#$.#$.#..#---
---#T#..$..#..$..#.##---
---#T#..*..#..*..#.#---.
---#*#############@#---.
---#T#..*..#..*..#.#----
---#T#..$..#..$..#.##---
---#T#..#$.#$.#$.#..#---
---#T*T.#..$..#..$..#---
---#TT@.#..*..#..*..#---
..-##################--.
..--------------------..

message mino also made a multi

..-----------..
.---#######---.
.---#@0#.@#---.
---##$t#t$##---
---#...$...#---
---###$$$###---
----#tt@tt#----
.---#######---.
...---------...
